
apply plugin: 'java'
apply plugin: 'maven'

group='com.github.jacek-marchwicki.key-value-db'
version=project.ext.versionName
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

configurations {
    deployerJars
}

dependencies {
    compile "javax.annotation:javax.annotation-api:1.2"
    compile "com.google.code.findbugs:jsr305:2.0.1"
    compile 'javax.inject:javax.inject:1'
    compile 'com.google.protobuf:protobuf-java:2.6.1'

    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.google.truth:truth:0.25'

    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}

test {
    include '**/*Test.class'
    maxParallelForks 1
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes ) {
    classifier 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "https://auto-close.appspot.com/maven/206") {
                authentication(userName: "un-important", password: project.ext.autoCloseToken)
            }
        }
    }
}